ext.tupleRange = (0..253)
ext.tuples = []

tupleRange.each { tuples << file("${generatedDir}/snacks/lang/Tuple${it + 1}.java") }

task (generateTuples, dependsOn: setUpGeneratedDir) {
    outputs.files tuples
    doLast {
        tupleRange.each {
            def size = it + 1;
            def range = (0..size - 1)
            def content = [
                'package snacks.lang;',
                '',
                'import static java.util.Arrays.asList;',
                'import static snacks.lang.SnackKind.TYPE;',
                '',
                'import java.util.Objects;',
                'import org.apache.commons.lang.builder.EqualsBuilder;',
                'import snacks.lang.type.RecordType;',
                'import snacks.lang.type.RecordType.Property;',
                'import snacks.lang.type.Type;',
                'import snacks.lang.type.VariableType;',
                '',
            ]

            content << "@Snack(name = \"Tuple$size\", kind = TYPE)"
            content << "public class Tuple$size<${range.collect { t -> "T$t" }.join(', ')}> {"

            content << ''
            content << '    @SnackType'
            content << '    public static Type type() {'
            content << "        return new RecordType(\"snacks.lang.Tuple$size\", asList("
            content << range.collect { t -> "            new Property(\"_$t\", new VariableType(\"snacks.lang.Tuple$size#_$t\"))" }.join(',\n')
            content << "        ));"
            content << '    }'

            content << ''
            range.each { f ->
                content << "    private final T$f _$f;"
            }

            content << ''
            content << "    public Tuple$size(${range.collect { t -> "T$t _$t"}.join(', ')}) {"
            range.each { f ->
                content << "        this._$f = _$f;"
            }
            content << "    }"

            content << ''
            content << '    @Override'
            content << '    public boolean equals(Object o) {'
            content << '        if (o == this) {'
            content << '            return true;'
            content << "        } else if (o instanceof Tuple$size) {"
            content << "            Tuple$size other = (Tuple$size) o;"
            content << '            return new EqualsBuilder()'
            range.each { f ->
                content << "                .append(_$f, other._$f)"
            }
            content << '                .isEquals();'
            content << '        } else {'
            content << '            return false;'
            content << '        }'
            content << '    }'

            range.each { f ->
                content << ''
                content << "    public T$f get_$f() {"
                content << "        return _$f;"
                content << "    }"
            }

            content << ''
            content << '    @Override'
            content << '    public int hashCode() {'
            content << "        return Objects.hash(${range.collect { t -> "_$t" }.join(', ')});"
            content << "    }"

            content << ''
            content << '    @Override'
            content << '    public String toString() {'
            content << "        return \"(Tuple$size \" + ${range.collect { t -> "_${t}" }.join(' + ", " + ')} + \")\";"
            content << '    }'
            content << '}'

            file(tuples.get(it)).write(content.join('\n'))
        }
    }
}

task(cleanTuples) {
    inputs.files tuples
    doLast {
        tuples.each { it.delete() }
    }
}

compileJava.dependsOn << generateTuples
clean.dependsOn << cleanTuples
